# IOM.ode

# This computer program was used in the publication "Phantom
# Bursting May Underlie Electrical Bursting in Single Pancreatic
# beta-Cells", J. Theoret. Biol., 501:110346, 2020. Authors are
# Mehran Fazli, Theodore Vo, and Richard Bertram.

# This is the Integrated Oscillator Model used to make the
# following figures:

# Fig. 3(A,B), Fig. 12(B): set gkca=800, vpdh=0.002 (fast bursting)
# Fig. 12(A), Fig. 13: set gkca=800, vpdh=0.03 (fast bursting)
# Fig. 3(C,D), Fig. 4, Fig. 11(B): set gkca=500, vpdh=0.002 (medium bursting)
# Fig. 3(E,F), Fig. 4, Fig. 11(B): set gkca=100, vpdh=0.002 (slow bursting)
# Fig. 13: set gkca=100, vpdh=0.03 (slow bursting)
# Has the simplified code for PFK.

# Variables:
#    v -- voltage (mV)
#    n -- activation of delayed rectifier
#    c -- free cytosolic calcium concentration (muM)
#    adp -- cytosolic ADP concentration (muM)
#    cer -- concentration of free calcium in the endoplasmic reticulum (muM)
#    cam -- mitocondrial calcium concentration (muM)
#    f6p -- fructose 6-phosphate concentration (muM)
#    fbp -- fructose 1,6-bisphosphate concentration (muM)

# Units:
# time: ms
# volume: l
# conductance: pS
# V: mV
# I: fA
# J: muM/ms
# alpha: mumol/fA/ms

# Initial conditions

v(0)=-60
n(0)=0
c(0)=0.07
cer(0)=139
cam(0)=27
adp(0)=789
f6p(0)=27
fbp(0)=78

##################################
## Electrical and Calcium Model ##
##################################

# Ca2+ current, ica
num gca=1000, vca=25

num vm=-20, sm=12
minf = 1/(1+exp((vm-v)/sm))

ica=gca*minf*(v-vca)


# Delayed-rectifying K+ current, ik
num gk=2700, vk=-75
num taun=20

num nin=-16, sn=5
ninf=1/(1+exp((nin-v)/sn))

ik=gk*n*(v-vk)

#  Ca2+-activated K+ current, ikca
num kd=0.5
par gkca=800

qinf=c^2/(kd^2+c^2)
ikca=-gkca*qinf*(vk-v) 

# K-ATP channel current, ikatp
num kdd=17, ktd=26, ktt=1
num atot=3000
par gkatpbar=25000

rad=sqrt(-4*adp^2+(atot-adp)^2)
atp=(atot+rad-adp)/2
mgadp=0.165*adp
adp3m=0.135*adp
atp4m=0.05*atp
topo=0.08+0.89*mgadp^2/kdd^2+0.16*mgadp/kdd 
bottomo=(1+mgadp/kdd)^2*(1+atp4m/ktt +adp3m/ktd) 
katpo=topo/bottomo 

ikatp=gkatpbar*katpo*(v-vk)

# Ca2+ fluxes across the plasma membrane
num alpha=5.18E-18, vcyt=1.15E-12, kpmca=0.2

Jmem=-(alpha/vcyt*ica + kpmca*c)

# Ca2+ fluxes into ER
num kserca=0.4, pleak=2.0E-4

Jer=kserca*c - pleak*(cer-c) 

# Ca2+ fluxes into the mitochondria
num kuni=0.4, knaca=0.001

Jm= kuni*c - knaca*(cam-c) 

# noise
par sigmaN=300
wiener w
noise = sigmaN*w

# The vector field (Electrical and Calcium Model)
num Cm=5300	
num fca=0.01
num sigmam=100
num sigmaer=31


v'=-(ica + ik + ikca + ikatp + noise)/Cm
n'=-(n-ninf)/taun
c'= fca*(Jmem - Jm - Jer)
cer'=fca*sigmaer*Jer
cam'=fca*sigmam*Jm

##################################
##        Metabolic Model       ##
##################################

# Glucokinase reaction rate
par Jgk=0.001

# PFK reaction rate, Jpfk

amp=adp^2/atp

#  Parameters
#  k1--Kd for AMP binding
#  k2--Kd for FBP binding
#  k3--Kd for F6P binding
#  k4--Kd for ATP binding


# famp = f13
# fatp = f41
# ffbp = f23
# fbt = f42
# fmt = f43

num k1=30, k2=1, k3=50000, k4=1000
num famp=0.02, fatp=20, ffbp=0.2, fbt=20, fmt=20
num kpfkbas=0.06, vpfk=0.01

ampk1 = amp/k1

fbpk2 = fbp/k2

f6p2k3 = f6p^2/k3

atp2k4 = atp^2/k4

wmax = ampk1*fbpk2*f6p2k3/(famp*ffbp)

wf6p = f6p2k3*(1 + atp2k4/fatp + fbpk2/ffbp + fbpk2/fbt*atp2k4/(ffbp*fatp) + ampk1/famp + ampk1/fmt*atp2k4/(famp*fatp) + ampk1/fmt*fbpk2/fbt*atp2k4/(famp*ffbp*fatp))

wnof6p = 1 + atp2k4 + fbpk2 + fbpk2/fbt*atp2k4 + ampk1 + ampk1/fmt*atp2k4 + ampk1*fbpk2 + ampk1/fmt*fbpk2/fbt*atp2k4

Jpfk= vpfk*(wmax + kpfkbas*wf6p)/( wmax + wf6p + wnof6p)

# PDH reaction rate, Jpdh
num kCaPDH=200
par vpdh=0.002

sinfty=cam/(cam+kCaPDH)

Jpdh=vpdh*sinfty*sqrt(fbp)

# The vector field (Metabolic Model)
num taua=300000

adp'=(atp-exp((1+2.2*Jpdh/(Jpdh+0.05))*(1-c/0.35))*adp)/taua
f6p'=0.3*(Jgk-Jpfk)
fbp'=(Jpfk-Jpdh/2)

aux noise=noise
aux itot=ica + ik + ikca + ikatp + noise
aux tsec=t/1000

############################################
#         XPP: numerical details	   #
############################################

@ meth=Euler, dt=0.1, total=10000
@ maxstor=20000000,bounds=10000000, xp=tsec, yp=v
@ xlo=0, xhi=10, ylo=-80, yhi=-10

done
