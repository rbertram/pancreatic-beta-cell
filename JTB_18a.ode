#
# JTB_18a.ode
#
# This XPPAUT file contains the Integrated Oscillator Model (IOM)
# program described in "Transitions between bursting modes in the
# integrated oscillator model for pancreatic beta-cells" by 
# Isabella Marinelli, Theodore Vo, Luca Gerardo-Giorda, and Richard Bertram.
# Has the simplified code for PFK.

# Variables:
#    v -- voltage (mV)
#    n -- activation of delayed rectifier
#    c -- free cytosolic calcium concentration (muM)
#    adp -- cytosolic ADP concentration (muM)
#    cer -- concentration of free calcium in the endoplasmic reticulum (muM)
#    cam -- mitocondrial calcium concentration (muM)
#    f6p -- fructose 6-phosphate concentration (muM)
#    fbp -- fructose 1,6-bisphosphate concentration (muM)

# Units:
# time: ms
# volume: l
# conductance: pS
# V: mV
# I: fA
# J: muM/ms
# alpha: mumol/fA/ms

# vpdh=0.009, gkca=50 gives slow bursting with pulsatile FBP
# vpdh=0.009, gkca=800 gives compound bursting with pulsatile FBP
# vpdh=0.002, gkca=50 gives slow bursting with sawtooth FBP

# Initial conditions

v(0)=-60 
n(0)=0
c(0)=0.1
cer(0)=185
cam(0)=100
adp(0)=780
f6p(0)=60
fbp(0)=40


##################################
## Electrical and Calcium Model ##
##################################

# Ca2+ current, ica
num gca = 1000, vca = 25

num vm=-20, sm=12
minf = 1/(1+exp((vm-v)/sm))

ica=gca*minf*(v-vca)


# Delayed-rectifying K+ current, ik
num gk=2700, vk=-75
num taun=20

num nin=-16, sn=5
ninf=1/(1+exp((nin-v)/sn))

ik=gk*n*(v-vk)

#  Ca2+-activated K+ current, ikca
num kd=0.5
par gkca=50

qinf=c^2/(kd^2+c^2)
ikca=-gkca*qinf*(vk-v) 

# K-ATP channel current, ikatp
num kdd=17, ktd=26, ktt=1
num atot=3000
par gkatpbar=25000

rad=sqrt(-4*adp^2+(atot-adp)^2)
atp=(atot+rad-adp)/2

aux atp=atp

mgadp=0.165*adp
adp3m=0.135*adp
atp4m=0.05*atp
topo=0.08+0.89*mgadp^2/kdd^2+0.16*mgadp/kdd 
bottomo=(1+mgadp/kdd)^2*(1+atp4m/ktt +adp3m/ktd) 
katpo=topo/bottomo 

ikatp=gkatpbar*katpo*(v-vk)

# Ca2+ fluxes across the plasma membrane
num alpha=5.18E-18, vcyt=1.15E-12, kpmca=0.2

Jmem=-(alpha/vcyt*ica + kpmca*c)

# Ca2+ fluxes into ER
num kserca=0.4, pleak=2.0E-4

Jer=kserca*c - pleak*(cer-c) 

# Ca2+ fluxes into the mitochondria
num kuni=0.4, knaca=0.001

Jm= kuni*c - knaca*(cam-c) 

# The vector field (Electrical and Calcium Model)
num Cm=5300	
num fca=0.01	
num sigmam=100
num sigmaer=31

v'=-(ica + ik + ikca + ikatp)/Cm
n'=-(n-ninf)/taun
c'= fca*(Jmem - Jm - Jer)
cer'=fca*sigmaer*Jer
cam'=fca*sigmam*Jm


##################################
##        Metabolic Model       ##
##################################

# Glucokinase reaction rate
par Jgk=0.001

# PFK reaction rate, Jpfk

amp=adp^2/atp

#  Parameters
#  k1--Kd for AMP binding
#  k2--Kd for FBP binding
#  k3--Kd for F6P binding
#  k4--Kd for ATP binding

num k1=30, k2=1, k3=50000, k4=1000
num famp=0.02, fatp=20, ffbp=0.2, fbt=20, fmt=20
num kpfkbas =0.06, vpfk=0.01

ampk1 = amp/k1

fbpk2 = fbp/k2

f6p2k3 = f6p^2/k3

atp2k4 = atp^2/k4

wmax = ampk1*fbpk2*f6p2k3/(famp*ffbp)

wf6p = f6p2k3*(1 + atp2k4/fatp + fbpk2/ffbp + fbpk2/fbt*atp2k4/(ffbp*fatp) + ampk1/famp + ampk1/fmt*atp2k4/(famp*fatp) + ampk1/fmt*fbpk2/fbt*atp2k4/(famp*ffbp*fatp))

wnof6p = 1 + atp2k4 + fbpk2 + fbpk2/fbt*atp2k4 + ampk1 + ampk1/fmt*atp2k4 + ampk1*fbpk2 + ampk1/fmt*fbpk2/fbt*atp2k4

Jpfk= vpfk*(wmax + kpfkbas*wf6p)/( wmax + wf6p + wnof6p)

# PDH reaction rate, Jpdh
num kCaPDH=200
par vpdh=0.009

sinfty= cam/(cam+kCaPDH)

Jpdh=vpdh*sinfty*sqrt(fbp)

# The vector field (Metabolic Model)
num taua= 300000

adp'=(atp-exp((1+2.2*Jpdh/(Jpdh+0.05))*(1-c/0.35))*adp)/taua
f6p'=0.3*(Jgk-Jpfk)
fbp'=(Jpfk-Jpdh/2)

############################################
#         XPP: numerical details	   #
############################################

@ meth=cvode, toler=1.0e-10, atoler=1.0e-10, dt=20.0, total=1200000
@ maxstor=20000,bounds=10000000, xp=tmin, yp=v
@ xlo=0, xhi=20, ylo=-70, yhi=-10

aux tmin=t/60000

done
