# ChayKeizer.ode
# A 3-dimensional Cahy-Keizer-like model. Based on the model by Chay and Keizer,
# "Minimal model for membrane oscillations in the pancreatic beta-cell", Biophysical
# Journal, 42:181-190, 1983.

v(0)=-65 
n(0)=0 
c(0)=0.1 

# Auto
par autoc=0, cknot=0.3

# where
ninf(v) = 1/(1+exp((vn-v)/sn))
minf(v) = 1/(1+exp((vm-v)/sm))

# set KATP conductance
par gkatp=180

num vk=-75, vl=-20, vca=25
num gk=2700, gl=0, gca=1000, cm=5300
num vm=-20, sm=12, vn=-16, sn=5, taun=20
param gkca=400
param kd=0.3, nh=3

# Ikca
ikca(v,c) = gkca/(1+(kd/c)^nh)*(v-vk)

# Calcium Handling
par kpmca=0.045, fcyt=0.001
num alpha=1.125e-6

# ICa
ica(v) = gca*minf(v)*(v-vca)

# Ca fluxes
Jmem = -(alpha*Ica(v) + kpmca*c)

# equations
dv/dt=(gl*(vl-v)+gk*n*(vk-v)- Ica(v) - Ikca(v,c) + gkatp*(vk-v))/cm
dn/dt=(ninf(v)-n)/taun
dc/dt = autoc*(cknot-c)+(1-autoc)*fcyt*Jmem

# track some currents
aux tsec=t/1000
aux Condkca=gkca/(1+(kd/c)^nh)
aux Ica=gca*minf(V)*(V-Vca)
aux Ik=gk*n*(V-Vk)
aux Curkca=Ikca(v,c)

@ meth=cvode, atol=1.0e-8, tol=1.0e-8, dt=10, total=150000, maxstor=100000
@ xp=tsec, yp=v, bound=100000000
@ xlo=0, xhi=150, ylo=-70, yhi=-10

done
