# Fig4.ode
#
# This XPPAUT file contains the code for the glycolytic oscillator used to make
# Fig. 4 in M.J. Merrins, R. Bertram, A. Sherman, L.S. Satin,
# "Phosphofructo-2-kinase/Fructose-2,6-bisphosphatase Modulates Oscillations
# of Pancreatic Islet Metabolism", PLoS One, 7(4):e34036,
# doi:10.1371/journal.pone.0034036, 2012. It has a parameter for the amount (in uM)
# of F26BP that is present.

# Variables:
# f6p -- glucose 6-phosphate
# fbp -- fructose 1,6-bisphosphate

# Actions:
" {f26bp=0} no F26BP
" {f26bp=0.1} some F26BP
" {f26bp=0.2} more F26BP

f6p(0)=200
fbp(0)=0
int_f6p(0)=0
int_fbp(0)=0

# Parameters
#  Jgk--glucokinase rate
#  atot--total adenine nucleotide concentration (micromolar)
#  k1--Kd for AMP binding
#  k2--Kd for FBP binding
#  k3--Kd for F6P binding
#  k4--Kd for ATP binding
#  f13,etc--Kd amplification factors for heterotropic binding
#  Jgpdh--glyceraldehyde phosphate dehydrogenase rate
#  speed--modifies period of glycolytic oscillations.
#  BIFrat--the ratio of VPFK2 to VFBPase2.

par atot=2500
par f26bp=0
par clampf6p=0
par clampfbp=0
par f6p0=0
par fbp0=0
par atp=800
par speed=2
par Jgk=0.2
par k4=330
num k1=30, k3=50000
par kfbp=1.0,kf26bp=0.15
num f13=0.02, f43=20, f23=0.2, f42=20, f41=20, pfkbas=0.06
par VmaxPFK=5
number phigk=0.3
num KGPDH=0.2

# glucose 6-phosphate
g6p = f6p/phigk
Jgpdh = KGPDH*sqrt(fbp)

# nucleotide concentrations, based on eqs. 14 and 15 from Smolen, ref. 1265.
rad = sqrt(1-4*(1/atp)*(atp-atot))
adp=0.5*(rad-1)*atp
amp = adp^2/atp
ratio = atp/adp

# Iterative calculation of PFK
# alpha=1 -- AMP bound
# beta=1 -- FBP bound
# gamma=1 -- F6P bound
# delta=1 -- ATP bound

# (alpha,beta,gamma,delta)
# (0,0,0,0)
weight1=1
topa1=0
bottom1=1

# (0,0,0,1)
weight2=atp^2/k4
topa2=topa1
bottom2=bottom1+weight2

# (0,0,1,0)
weight3=f6p^2/k3
topa3=topa2+weight3
bottom3=bottom2+weight3

# (0,0,1,1)
weight4=(f6p*atp)^2/(f43*k3*k4)
topa4=topa3+weight4
bottom4=bottom3+weight4

# (0,1,0,0)
# weight5=fbp/k2
weight5=(fbp/kfbp)+(f26bp/kf26bp)
topa5=topa4
bottom5=bottom4+weight5

# (0,1,0,1)
# weight6=(fbp*atp^2)/(k2*k4*f42)
weight6=((fbp/kfbp)+(f26bp/kf26bp))*(atp^2)/(k4*f42)
topa6=topa5
bottom6=bottom5+weight6

# (0,1,1,0)
# weight7=(fbp*f6p^2)/(k2*k3*f23)
weight7=((fbp/kfbp)+(f26bp/kf26bp))*(f6p^2)/(k3*f23)
topa7=topa6+weight7
bottom7=bottom6+weight7

# (0,1,1,1)
# weight8=(fbp*f6p^2*atp^2)/(k2*k3*k4*f23*f42*f43)
weight8=((fbp/kfbp)+(f26bp/kf26bp))*(f6p^2*atp^2)/(k3*k4*f23*f42*f43)
topa8=topa7+weight8
bottom8=bottom7+weight8

# (1,0,0,0)
weight9=amp/k1
topa9=topa8
bottom9=bottom8+weight9

# (1,0,0,1)
weight10=(amp*atp^2)/(k1*k4*f41)
topa10=topa9
bottom10=bottom9+weight10

# (1,0,1,0)
weight11=(amp*f6p^2)/(k1*k3*f13)
topa11=topa10+weight11
bottom11=bottom10+weight11

# (1,0,1,1)
weight12=(amp*f6p^2*atp^2)/(k1*k3*k4*f13*f41*f43)
topa12=topa11+weight12
bottom12=bottom11+weight12

# (1,1,0,0)
# weight13=(amp*fbp)/(k1*k2)
weight13=(amp/k1)*((fbp/kfbp)+(f26bp/kf26bp))
topa13=topa12
bottom13=bottom12+weight13

# (1,1,0,1)
# weight14=(amp*fbp*atp^2)/(k1*k2*k4*f42*f41)
weight14=((fbp/kfbp)+(f26bp/kf26bp))*(amp*atp^2)/(k1*k4*f42*f41)
topa14=topa13
bottom14=bottom13+weight14

# (1,1,1,0) -- the most active state of the enzyme
# weight15=(amp*fbp*f6p^2)/(k1*k2*k3*f23*f13)
weight15=((fbp/kfbp)+(f26bp/kf26bp))*(amp*f6p^2)/(k1*k3*f23*f13)
topa15=topa14
topb=weight15
bottom15=bottom14+weight15

# (1,1,1,1)
# weight16=(amp*fbp*f6p^2*atp^2)/(k1*k2*k3*k4*f23*f13*f42*f41*f43)
weight16=((fbp/kfbp)+(f26bp/kf26bp))*(amp*f6p^2*atp^2)/(k1*k3*k4*f23*f13*f42*f41*f43)
topa16=topa15+weight16
bottom16=bottom15+weight16

# Phosphofructokinase rate
% rate=(pfkbas*VmaxPFK*topa16 + VmaxPFK*topb)/bottom16  
% pfk=rate*(atp/(atp+2))    atp saturates
Jpfk=(pfkbas*VmaxPFK*topa16 + VmaxPFK*topb)/bottom16

# ODEs
f6p' = clampf6p*(f6p0-f6p)+(1-clampf6p)*speed*phigk*(Jgk - Jpfk)
fbp' = clampfbp*(fbp0-fbp)+(1-clampfbp)*speed*(Jpfk - 0.5*Jgpdh)

# Mean substrate and product
int_f6p' = f6p
int_fbp' = fbp
aux mean_f6p=int_f6p/(t+0.0001)
aux mean_fbp=int_fbp/(t+0.0001)

aux tmin=t/60
aux ttrans=(t-6000)/60
aux JGPDH=Jgpdh
aux atp=atp
aux Jpfk=Jpfk

@ meth=cvode, toler=1.0e-10, atoler=1.0e-10, dt=0.5, total=9000, maxstor=70000
@ bounds=10000000, xp=tmin, yp=fbp, nmesh=100
@ xlo=0, xhi=150, ylo=0, yhi=50

