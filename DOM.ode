# DOM.ode
#
# The Dual Oscillator Model from Bertram, Satin, Zhang, Smolen, and Sherman, Biophysical Journal
# 87:3074-3087, 2004.
# Has the simplified code for PFK.

v(0)=-60
n(0)=0
c(0)=0.1
adp(0)=780
cer(0)=185
f6p(0)=51
fbp(0)=40

# Parameter vlaues for various behaviors:
#
# Compound bursting: Jgk=0.4, gkatp=26000, g6ca=500
# Accordion bursting: Jgk=0.4, gkatp=23000, gkca=500

# ----------------------------------------------------------------
# Membrane and Ca components

# sigmav=cyt volume/ER volume
num pleak=0.0002, sigmav=31
num kserca=0.4
num lambdaer=1, epser=1
num vm=-20,sm=12,vn=-16,sn=5,taun=20

# where
minf = 1/(1+exp((vm-v)/sm))
ninf = 1/(1+exp((vn-v)/sn))

num vk=-75, vca=25
num gk=2700, cm=5300
num gca=1000
num kd=0.5, nh=2

param gkca=600

# Ikca
ikca = gkca/(1+(kd/c)^nh)*(v-vk)

# Calcium Handling
num alpha=4.50e-6, kpmca=0.2, fcyt=0.01, fer=0.01

# Ikatp
par gkatpbar=25500

# ICa
ica = gca*minf*(v-vca)

# Ik
Ik = gk*n*(v-vk)

# IKATP
# ikatp = freezeatp*gkatpstar*(v-vk) +
# (1-freezeatp)*gkatpbar*(v-vk)*(1.0+d/kone)/(1.0+d/kone+(tot-d)/ktwo)

# Ca fluxes
Jmem = -(alpha*Ica + kpmca*c)

Jserca = kserca*c
Jleak = pleak*(cer - c)
Jer = epser*(Jleak - Jserca)/lambdaer

# -----------------------------------------------------
# Glycolytic and Keizer-Magnus components

# f6p -- fructose 6-phosphate
# fbp -- fructose 1,6-bisphosphate

# Parameters
#  Jgk--glucokinase rate
#  atot--total adenine nucleotide concentration (micromolar)
#  k1--Kd for AMP binding
#  k2--Kd for FBP binding
#  k3--Kd for F6P binding
#  k4--Kd for ATP binding
#  famp,etc--Kd amplification factors for heterotropic binding
#  Jpdh--glyceraldehyde phosphate dehydrogenase rate

# Glycolytic parameters
par kg=10
par Jgk=0.4
num atot=3000
num k1=30, k2=1, k3=50000, k4=1000
num famp=0.02, fatp=20, ffbp=0.2, fbt=20, fmt=20, kpfkbas=0.06
number cat=2
num katpase=0.0003
!kgpi=10./3.
# convert time from sec to msec
num convert=0.001

# lower glycolysis
p klg=0.3
# Glycolytic expressions
par vpdh=0.4
Jpdh = vpdh*sqrt(fbp)

# Calculation of PFK rate

par vpfk=20

ampk1 = amp/k1

fbpk2 = fbp/k2

f6p2k3 = f6p^2/k3

atp2k4 = atp^2/k4

wmax = ampk1*fbpk2*f6p2k3/(famp*ffbp)

wf6p = f6p2k3*(1 + atp2k4/fatp + fbpk2/ffbp + fbpk2/fbt*atp2k4/(ffbp*fatp) + ampk1/famp + ampk1/fmt*atp2k4/(famp*fatp) + ampk1/fmt*fbpk2/fbt*atp2k4/(famp*ffbp*fatp))

wnof6p = 1 + atp2k4 + fbpk2 + fbpk2/fbt*atp2k4 + ampk1 + ampk1/fmt*atp2k4 + ampk1*fbpk2 + ampk1/fmt*fbpk2/fbt*atp2k4

Jpfk= vpfk*(wmax + kpfkbas*wf6p)/( wmax + wf6p + wnof6p)

# KATP channel
# Dissociation constants (from Magnus and Keizer, 1998, ref.
# 1264)
num kdd=17, ktd=26, ktt=1
par r=1
num vg=2.2, r1=0.35, taua=300000

# Functions

# nucleotide concentrations, based on eqs. 14 and 15 from Smolen, ref. 1265.
rad = sqrt((adp-atot)^2-4*adp^2)
atp = 0.5*(atot-adp+rad)
amp = adp^2/atp
ratio = atp/adp

% KATP channel open probability (from Magnus and Keizer, 1998, ref. 1264)
mgadp = 0.165*adp
adp3m = 0.135*adp
atp4m = 0.05*atp
topo = 0.08*(1+2*mgadp/kdd) + 0.89*(mgadp/kdd)^2
bottomo = (1+mgadp/kdd)^2 * (1+adp3m/ktd+atp4m/ktt)
katpo = topo/bottomo
ikatp = gkatpbar*katpo*(v-vk)

# glycolytic feedback onto adp
y = vg*(Jpdh/(kg+Jpdh))
fback = r+y



# ------------------------------------------------------
# Differential equations

v' = -(Ik + Ica + Ikca + Ikatp)/cm
n' = (ninf-n)/taun
c' = fcyt*(Jmem + Jer)
adp' = (atp-adp*exp(fback*(1-c/r1)))/taua 
cer' = -fer*sigmav*Jer
f6p'=convert*(1/(1+kgpi)*(Jgk-jpfk))
fbp'=convert*1/(1+klg)*(jpfk-.5*jpdh)


@ meth=cvode, toler=1.0e-10, atoler=1.0e-10, dt=10.0, total=1200000,
@ maxstor=20000,bounds=10000000, xp=tmin, yp=v
@ xlo=0, xhi=20, ylo=-70, yhi=-10

# aux itot=(ica + ik + ikatp  + ikca + icrac)/1000
aux tsec=t/1000
aux tmin=t/60000
% aux w=omega
aux GKCA=gkca/(1+(kd/c)^nh)
aux GKATP=gkatpbar*katpo
aux Atp=atp/1000
aux ratio=atp/adp
% aux Katpo=katpo
done
